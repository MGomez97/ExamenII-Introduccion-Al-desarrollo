Algoritmo ExamenII
    Definir a, b, c, discriminante, x1, x2 como reales
    Definir opcion, numeroTabla, i como entero
	
    // Ciclo principal del menú
    Repetir
        Escribir "----------- MENÚ -----------"
        Escribir "-1. Fórmula cuadrática     -"
        Escribir "-2. Tablas de multiplicar  -"
        Escribir "-3. Salir                  -"
        Escribir "----------------------------"
        Escribir "Ingrese una opción: "
        Leer opcion
		
        Segun opcion Hacer
            Caso 1:
                // fórmula cuadrática
                Escribir "Ingrese el valor de a: "
                Leer a
                Si a = 0 Entonces
                    Escribir "Error: a debe ser distinto de cero para la fórmula cuadrática."
                Sino
                    Escribir "Ingrese el valor de b: "
                    Leer b
                    Escribir "Ingrese el valor de c: "
                    Leer c
					
                    // Validación del discriminante
                    discriminante = b^2 - 4*a*c
                    Si discriminante < 0 Entonces
                        Escribir "El discriminante es negativo, la raíz cuadrada no está definida en los reales."
                    Sino
                        x1 = (-b + raiz(discriminante)) / (2*a)
                        x2 = (-b - raiz(discriminante)) / (2*a)
                        Escribir "Las soluciones son:"
                        Escribir "x1 = ", x1
                        Escribir "x2 = ", x2
                    FinSi
                FinSi
				esperar 2 Segundos
				LimpiarPantalla
				
            Caso 2:
                // Multiplicacion de tablas
                Escribir "Ingrese el número de tabla que desea ver: "
                Leer numeroTabla
				
                Escribir "Tabla del ", numeroTabla
                Para i <- 1 Hasta 10 Con Paso 1 Hacer
                    Escribir numeroTabla, " x ", i, " = ", numeroTabla * i
                FinPara
                //Pausa()
				
            Caso 3:
                Escribir "Saliendo del programa..."
                esperar 1 Segundos
				
            De Otro Modo:
                Escribir "Opción no válida. Por favor, ingrese una opción válida."
                Esperar Tecla
        FinSegun
    Hasta Que opcion = 3
FinAlgoritmo
